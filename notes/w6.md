# **[CS 2110] - Week 6: Assembly & Assembly Programming**
## Assembly
### Assembly Language
- low-level language
- dependent on instruction set architecture (ISA)
- typically each instruction line in assembly language produces a single machine instruction
- contrasts with high-level languages
	- FORTRAN, C, etc.
	- typically converted to assembly
- user friendly
	- mnemonics, not binary numbers
	- names for memory addresses and opcodes
- does a bit more than just the machine instructions

### Our Choices
- choices of approach
	- assembly language programming for experienced high-level language programmers
- choice of goal
	- a basic competency in assembly language for use as a tool in debugging, computer architecture, and optimization

### Example: Multiply By 6
![[../img/w6-ss1-multiply-by-6-assembly-language.png|500]]

### Assembler Directives
- **assembler directives**: pseudo-ops that tell the assembler to do something
- ex. ```.orig x3050``` or ```.blkw 1``` or ```.fill x0006``` or ```.end```

### Labels
- **tags/labels**: lets you assign names to memory addresses, even if they move around
- ex. ```AGAIN``` or ```SIX```
- Note: tags/labels are optional

### Opcodes And Operands
- ex. ```ADD	R3, R3, R2```
- ex. ```ADD	R1, R1, #-1```

### Comments
- ex. ```; Summing into R3```

### Number Bases
|Base|Denoting|
|-|-|
|decimal|#|
|binary|b|
|hexadecimal|x|

- ex. ```#-1``` or ```b1101``` or ```x3050```

### What Gets Assembled (What Complx Shows)
![[../img/w6-ss2-what-gets-assembled.png|600]]

### Assembler Directive
- **assembler directive**: tells the assembler to do something other than assemble a machine instruction
- pseudo-ops
	- ```.orig x3000```
		- tells the assembler to put a block of code at the desired address (in this example, x3000)
	- ```.fill 1```
		- initialize one location
		- reserve and fill a word with a numeric value, optionally naming it (in this example, puts 1 in the current memory location)
	- ```.blkw n```
		- set aside n words in memory
		- reserve and optionally name the beginning of an area of memory
	- ```.stringz "sample"```
		- initialize 7 locations (one location for every letter in "sample" and a 0 word '\\0')
		- store an ASCII string in memory with a zero terminator, optionally naming it
	- ```.end```
		- end of assembly program or block

### Assembly File Layout
```
.orig x3000
		LD	R1, B
		LD	R0, A
		HALT
A		.fill 2
ARRAY	.fill x6000
.end

.orig x6000
		.fill 1
		.fill 2
		.fill 3
.end
```
- the above code is laid out in the LC-3 like the following 

|Label|Address|Value|
|-|-|-|
||x2FFE|don't know|
||x2FFF|don't know|
||x3000|LD R1, B|
||x3001|LD R0, A|
||x3002|HALT|
||x3003|2|
||x3004|x6000|
||x3005|don't know|
||||
||x5FFE|don't know|
||x5FFF|don't know|
||x6000|1|
||x6001|2|
||x6002|3|
||x6003|don't know|
||x6004|don't know|

### Common Assembly Techniques
- use a semicolon to write comments
- clearing a register
	- ex. clearing R1 ```AND R1, R1, 0```
- if-statements and while loops use BR statements to conditionally jump
	- ex. BRz will branch when condition codes are 0
	- Note: BR and BRnzp will branch unconditionally
- setting condition codes	
	- ex. ```ADD R1, R1, 0; R1=R1+0```
- subtraction/negation - use NOT (flip the bits) and ADD (add 1)
```
NOT R3, R1		;R3=~R1
ADD R3, R3, 1	;R3=~R1+1=-R1
ADD R3, R2, R3	;R3=R2+(-R1)=R2-R1
```
- copy a value to another register
	- ```ADD R2, R1, 0	;R2=R1+0```

### The Assembly Process
- objective
	- translate the assembly language (AL) program into machine language (ML)
	- each AL instruction yields one ML instruction word
	- interpret pseudo-ops correctly
- problem
	- an instruction may reference a label
	- if the label hasn't been encountered yet, the assembler can't form the instruction word
- solution
	- two-pass assembly

### Two-Pass Assembly
- first pass - generating the symbol table
	- scan each line
	- keep track of current address (location counter)
		- increment by 1 for each instruction
		- adjust the location counter as required for any pseudo-ops (ex. ```.fill```, ```.blkw```, etc.)
	- for each label, enter it into the symbol table along with the current address (location counter)
	- stop when ```.end``` command is encountered
- second pass - generating the ML program
	- scan each line again
	- translate each AL instruction into ML
		- look up symbols in the symbol table
		- ensure that labels are no more than +256 / -255 lines from PCoffset9 instructions
		- calculate the operand field for the instruction
		- update the current address (location counter)
	- fill memory locations as directed by pseudo-ops
	- stop when ```.end``` is encountered

![[../img/w6-ss3-symbol-table.png|600]]
![[../img/w6-ss4-assembled-code.png|600]]

### Aliases: TRAP Pseudo-Instruction
- the assembler will recognize aliases for certain predefined TRAP instructions
- TRAPs are subroutines build into the LC-3 to simplify instructions
- look like normal instructions but are aliases for TRAP calls
	- ex. ```HALT``` is the same as ```TRAP x25```

![[../img/w6-ss5-trap-instruction.png]]

|instruction|alias|action|
|-|-|-|
|TRAP x25|HALT|stops the CPU|
|TRAP x23|IN|input character from keyboard|
|TRAP x21|OUT|print character on screen|
|TRAP x22|PUTS|
|TRAP x24|PUTSP|
|TRAP x20|GETC|
|JMP R7|RET|

---
## Assembly Programming
### What Can You Do With ADD, AND, NOT?
- add
- and
- not
- subtract
- or
- clear a register
- copy from one register to another
- increment a register by n, $-16\leq n\leq 15$

### How Can We Subtract?
```
;SUB R1, R2, R3	;instruction does not exist
;how to subtract

NOT R3, R3		;flip the bits of R3
ADD R3, R3, #1	;add 1 to R3 ;now R3 is -R3
ADD R1, R2, R3
```

### How Can We OR?
```
;OR R1, R2, R3	;instruction does non exist: R1 = R2|R3
;how to do OR	;use DeMorgan's Law

NOT R2, R2		;R2 = ~R2
NOT R3, R3		;R3 = ~R3
AND R1, R2, R3	;R1 = ~R2 & ~R3
NOT R1, R1		;R1 = ~(~R2 & ~R3)
```

### How Can We Clear?
```
;CLR R1			;ex. set R1 = 0
AND R1, R1, #0	;R1 = R1 & 0
```

### How Can We Move?
```
;MOV R1, R2		;ex. R1 = R2
ADD R1, R2, #0	;R1 = R2 + 0
```
- this is how you copy from one register to another
- Note: do not use LD instruction to move, it will not work

### What Can We Do With A BR?
- if, then, else
- for
- while
- do while
- conditional branch
- unconditional branch (BRnzp or BR)
- never branch (NOP)

### What Can We Do With JMP?
- go to some location
- branches long distances
- Note: we rarely use JMP in this course, BR is more common

### Differences Between BR And JMP
- BR
	- can branch on N, Z, and P conditions
	- can always branch (BR or BRnzp)
	- can never branch (NOP)
	- destination address is always PCoffset9
	- cant branch more than -256 to 255 words
- JMP
	- always branches
	- destination address is always in a register
	- can branch to any memory address

### How Do We Do IF Using A BR (branch)?
- first do an operation (ADD, AND, NOT, LD, LDI, LDR, ST, STI, STR) to set the condition codes
- then BR with the appropriate combination of NZP conditions in the instruction

### Examples Of Every Condition - Comparison With Zero
- Note: think N is $<$, Z is $==$, P is $>$

|comparison|condition code|
|-|-|
|<|N|
|<=|NZ|
|==|Z|
|!=|NP|
|>=|ZP|
|>|P|

- always => NZP (we abbreviate BRnzp as just BR, branch always)
- never => no condition codes (we use NOP = no operation)

### Stylized Assembly Coding
- assembly programming gives the programmer a lot of freedom
	- freedom to optimize performance whether needed or not
	- freedom to write impossibly complex code
	- freedom to make a zillion different errors
	- freedom to debug everything in binary
- is this always a good idea?
	- if you are just learning
	- if you don't want to make a career of it
	- if you recognize that a compiler/optimizer can do a better job

### Suggested Convention: Act Like A Compiler
- write your algorithm in a high-level language
- write down where you are going to store your variables (register, static memory, stack offset, etc.)
	- use comments for registers and stack offsets
	- use assembler directives to reserve memory
- copy your algorithm with ";" at the beginning of each line to create assembly language comments
- translate each statement into a stanza of the appropriate machine language instructions
	- for complex statements like IF, WHILE, CALL, use templates
	- make sure that each stanza you translate is independent of your other stanzas
		- at the start, don't depend on the contents of temporary registers
		- at the start, don't depend on the condition codes
		- at the end, store your modified values where they belong
- this will not result in the most efficient code but it will be correct code
- Note: Complx doesn't know what is data and what is an instruction code, you need to differentiate while writing code

### if (R1>0) then ... else ...
```
		ADD		R1, R1, #0		;if (R1>0) then
		BRnz	ELSE1

		...[THEN part]...

		BR		ENDIF1
ELSE1	NOP						;else

		... [ELSE part]

ENDIF1	NOP						;endif
```

### for (init; R1>0; reinit)
```
		...[initialize loop]...	;for (init;

FOR1	ADD 	R1, R1, #0
		BRnz 	ENDF1			;R1>0;
	
		...[FOR body]...
		
		...[reinitialize]...	;reinit)
	
		BR		FOR1
ENDF1	NOP
```

### while (R1>0)
```
WHILE1	ADD 	R1, R1, #0		;while(R1>0)
		BRnz 	ENDW1
		
		...[WHILE body]...
		
		BR		WHILE1
ENDW1	NOP						;endwhile
```

### do ... while(R1>0)
```
DO1		NOP						;do

		...[DO WHILE body]...
		
		ADD 	R1, R1, #0
		BRp		DO1				;while(x)
```

### if (A=0 && B=0)
```
		; go to ELSE part if either condition is false
		
		LD		R0, A
		BRnp	ELSE
		LD		R0, B
		BRnp	ELSE2
		
		;THEN code goes here
		
		BR		ENDIF2
ELSE2	NOP

		;ELSE code goes here
		
ENDIF2	NOP
```

### Array Addressing
```
;i is R3, q is R4
;short q

;short a[5]
a		.blkw	5

;short i=3
		AND 	R3, R3, #0
		ADD		R3, R3, #3

;q=a[2]
		LEA		R0, a
		LDR		R4, R0, #2
		
;q=a[i];array is close
		LEA		R0, a
		ADD		R0, R0, R3
		LDR		R4, R0, #0
		
;q=a[i];array is far
		LD		R0, aaddr
		ADD		R0, R0, R3
		LDR		R4, R0, #0
		
		...
		
		aaddr	.fill a
		
;a[i]=q
		LEA		R0, a
		ADD		R0, R0, R3
		SR		R4, R0, #0

```


### Templates For Indexing Into An Array
#### Fetching ARRAY\[I\] (nearby)

```
LEA	R1, ARRAY	;R1 is address of ARRAY[0]
LD	R2, I		;R2 is index number
ADD	R1, R1, R2	;R1 is address of ARRAY[I]
LDR R1, R1, #0	;R1 is value of ARRAY[I]
```

#### Fetching ARRAY\[I\] (far away)
```
LD	R1, AD		;R1 is address of ARRAY[0]
LD	R2, I		;R2 is index number
ADD	R1, R1, R2	;R1 is address of ARRAY[I]
LDR	R1, R1, #0	;R1 is value of ARRAY[I]
BR	SK			;Don’t execute the address
AD	.fill ARRAY
SK	NOP
```

#### Storing ARRAY\[I\] (nearby)
```
LEA R1, ARRAY	;R1 is address of ARRAY[0]
LD R2, I		;R2 is index number
ADD R1, R1, R2	;R1 is address of ARRAY[I]
STR R3, R1, #0	;value of ARRAY[I] is R3
```

#### Storing ARRAY\[I\] (far away)
```
LD R1, AD 		;R1 is address of ARRAY[0]
LD R2, I		;R2 is index number
ADD R1, R1, R2	;R1 is address of ARRAY[I]
STR R3, R1, #0	;value of ARRAY[I] is R3
BR	SK			;Don’t execute the address
AD	.fill ARRAY
SK	NOP
```